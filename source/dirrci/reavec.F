C
      SUBROUTINE REAVEC (VECTR,VECTI,DR,DI,ILIND)
C
      IMPLICIT REAL*8 (A-H, O-Z)
C
      INCLUDE 'param.inc'
      INCLUDE 'files.inc'
      INCLUDE 'general.inc'
      INCLUDE 'iterat.inc'
C
      DIMENSION VECTR(NDET,*),VECTI(NDET,*),DR(*),DI(*)
      DIMENSION ILIND(*)
      DIMENSION IDREC(N8)
C
#if defined (BIT64)
      OPEN (MRCFINV,FILE=FNAM(5),ACCESS='DIRECT',RECL=8)
#else
      OPEN (MRCFINV,FILE=FNAM(5),ACCESS='DIRECT',RECL=4)
#endif
      READ (MRCFINV,REC=1,ERR=10000) LENREC
      CLOSE (MRCFINV)
      OPEN (MRCFINV,FILE=FNAM(5),ACCESS='DIRECT',RECL=LENREC)
      READ (MRCFINV,REC=1,ERR=10000) LENREC,NPDET,NPROOT,
     &                     (EIGEN1(IROOT),IROOT=1,NPROOT),
     &                     (IDREC(IROOT),IROOT=1,NPROOT)
      IF (NPDET.NE.NDET.OR.NPROOT.LT.NROOTS) GOTO 10000
      DO 20 IROOT = 1, NROOTS
      EIGEN2(IROOT) = EIGEN1(IROOT)
      WRITE (6,1000) IROOT,EIGEN1(IROOT)
      IVEC = IROOT
      CALL ZZERO (NDET,VECTR(1,IVEC))
      IF (.NOT.REALAR) CALL ZZERO (NDET,VECTI(1,IVEC))
      IREC = IDREC(IROOT)
  10  IF (REALAR) THEN
        READ (MRCFINV,REC=IREC,ERR=10000) INREC, NONZERO,
     &       (ILIND(IDNZ),IDNZ=1,NONZERO),
     &       (DR(IDNZ),IDNZ=1,NONZERO)
      ELSE
        READ (MRCFINV,REC=IREC,ERR=10000) INREC, NONZERO,
     &       (ILIND(IDNZ),IDNZ=1,NONZERO),
     &       (DR(IDNZ),DI(IDNZ),IDNZ=1,NONZERO)
      ENDIF
      DO 11 IDNZ = 1, NONZERO
         VECTR(ILIND(IDNZ),IVEC) = DR(IDNZ)
  11     CONTINUE
      IF (.NOT.REALAR) THEN
         DO 12 IDNZ = 1, NONZERO
            VECTI(ILIND(IDNZ),IVEC) = DI(IDNZ)
  12     CONTINUE
      ENDIF
      IF (INREC.NE.0) THEN
         IREC = INREC
         GOTO 10
      ENDIF
      NVEC = IVEC
      CALL ORTHN (VECTR,VECTI)
  20  CONTINUE
      CLOSE (MRCFINV,STATUS='KEEP')
C
      RETURN
 1000 FORMAT (/' Vector #',I4,' read from MRCFINV; Eigenvalue ',F20.12)
10000 CALL Q2JOB (2,'ERROR READING MRCFINV',' ',0)
      RETURN
      END
