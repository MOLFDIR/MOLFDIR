
      SUBROUTINE PMMMHM(NN,N,AR,AI,M,HR,HI,MM,BR,BI)
      IMPLICIT REAL*8 (A-H, O-Z)
C
C     ======================================================
C     MULTIPLY RECTANGULAR COMPLEX*16 MATRIX A(N,M) WITH
C     LOWER TRIANGLE OF HERMITIAN MATRIX H(M,M) AND PUT THE
C     RECTANGULAR RESULT IN B(N,M)
C     NN--FIRST DIMENSION OF RECTANGULAR MATRICES AR AND AI
C         AS DECLARED IN THE CALLING PROGRAM
C     N---FIRST DIMENSION OF RECTANGULAR MATRICES AR,AI,BR,BI
C     AR--REAL PART OF RECTANGULAR MATRIX A
C     AI--IMAGINARY PART OF RECT.  MATRIX A
C     M---SECOND DIMENSION OD RECTANGULAR MATRICES AR...BI,AND
C         OF THE HERMITIAN MATRIX H
C     HR--REAL PART OF HERMITIAN MATRIX H
C     HI--IMAGINARY PART OF HERM.MATRIX H
C     MM--FIRST DIMENSION OF THE RECTANGULAR MATRICES BR AND BI
C         AS DECLARED IN THE CALLING PROGRAM
C     BR--REAL PART OF THE RESULT MATRIX
C     BI--IMAGINARY PART OF THE RESULT MATRIX
C     THE MATRICES HR AND HI ARE TO BE GIVEN AS LINEAR ARRAY'S
C     ========================================================
      DIMENSION AR(NN,M),AI(NN,M),BR(MM,M),BI(MM,M)
      DIMENSION HR(M*(M+1)/2),HI(M*(M+1)/2)
      DO 40 I=1,M
      DO 30 K=1,N
      SUMR=0.0
      SUMI=0.0
      L=I*(I-1)/2
      DO 10 J=1,I
      L=L+1
      SUMR=SUMR+AR(K,J)*HR(L)+AI(K,J)*HI(L)
      SUMI=SUMI-AR(K,J)*HI(L)+AI(K,J)*HR(L)
   10 CONTINUE
      DO 20 J=I+1,M
      L=L+J-1
      SUMR=SUMR+AR(K,J)*HR(L)-AI(K,J)*HI(L)
      SUMI=SUMI+AR(K,J)*HI(L)+AI(K,J)*HR(L)
   20 CONTINUE
      BR(K,I)=SUMR
      BI(K,I)=SUMI
   30 CONTINUE
   40 CONTINUE
C     PRINT*,'BR = ',BR
C     PRINT*,'BI = ',BI
      RETURN
      END
