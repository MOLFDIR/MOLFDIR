C
      SUBROUTINE DIIS(FCR,FCI,FOR,FOI,FOCR,FOCI,DCR,DCI,DOR,DOI,
     +           DOCR,DOCI,SCRR1,SCRI1,SCRR2,SCRI2,SCRR3,SCRI3)
      IMPLICIT REAL*8 (A-H,O-Z)
C
C === THIS ROUTINE WILL SET UP THE DIIS EQUATIONS AND SOLVE THEM
C
C === THE DIIS METHOD USED HERE IS TAKEN FROM :
C ===                       TRACY P. HAMILTON AND PETER PULAY,
C ===                       J. CHEM. PHYS., 84(10, 5728, 15 MAY 1986
C
C === THE ERROR VECTOR AND THE FOCK MATRIX ARE WRITTEN TO THE FILE DIISFIL
C
C === DEFINE THE NUMBER OF FOCK MATRICES RETAINED
C
      INCLUDE 'paracom.inc'
      INCLUDE 'general.inc'
      INCLUDE 'diis.inc'
C
      REAL*8 FCR(*), FCI(*), FOR(*), FOI(*), FOCR(*), FOCI(*)
      REAL*8 DCR(*), DCI(*), DOR(*), DOI(*), DOCR(*), DOCI(*)
      REAL*8 SCRR1(*),SCRI1(*),SCRR2(*),SCRI2(*),SCRR3(*),SCRI3(*)
C
      LOGICAL DFEX, CURRENT
C
C === IF THIS IS THE FIRST TIME DIIS IS CALLED DURING THIS RUN
C === WE HAVE TO INITIALIZE DIMAT AND MAYBE THE COUNTERS
C
      IF (DFIRST) THEN
        WRITE(6,*)
        WRITE(6,*)
        WRITE(6,*) ' *** DIIS METHOD ENTERED ***'
        WRITE(6,*)
        WRITE(6,*)
        DFIRST = .FALSE.
        IDREC = MAX(LREC,N30*N30+4)
C === CHECK IF DIISFIL EXISTS
        DFEX = .FALSE.
        INQUIRE(FILE='DIISFIL', EXIST=DFEX)
C === IF SO READ IN DIMAT AND SOME COUNTERS AND CHECK
C === IF DIISFIL IS STILL CURRENT
        OPEN (DIISFIL, FILE='DIISFIL', FORM='UNFORMATTED',
     +        ACCESS='DIRECT',RECL=16 * IDREC)
        IF (DFEX) THEN
          CALL RDIIS(ICCYC,DIMAT,DIMAT,1,1,6)
C === ICCYC IS THE TOTAL ITERATION COUNT BELONGING TO THE LAST FOCK MATRIX
C === THERE IS ONE COMPLICATION : THE VECTORS ICCYC + 1 COME FROM DIAGONALIZING
C === THE FOCK MATRIX ICCYC
C === WRITTEN TO DIISFIL
C === ICCYC >= ICYCLS - 1
          CURRENT = .FALSE.
          IF (ICCYC .LE. (ICYCLS - 1)) THEN
          CURRENT = .TRUE.
          ENDIF
        ENDIF
        IF (.NOT. (DFEX .AND. CURRENT)) THEN
C === IF DIISFIL DOES NOT EXIST OR IS NOT CURRENT WE START A NEW
C === INITIALIZE COUNTERS
        IFIRST = 1
        ILAST = 0
        IDCYC = 0
        ENDIF
      ELSE
C === READ IN THE DIIS MATRIX
        OPEN (DIISFIL, FILE='DIISFIL', FORM='UNFORMATTED',
     +        ACCESS='DIRECT',RECL=16 * IDREC)
        CALL RDIIS(1,DIMAT,DIMAT,1,1,5)
      ENDIF
      IF (LASTRUN) THEN
      CLOSE(DIISFIL,STATUS='DELETE')
      UDIIS = .FALSE.
      WRITE(6,*)
      WRITE(6,*)
      WRITE(6,*) ' *** DIIS METHOD EXITED *** '
      WRITE(6,*)
      WRITE(6,*)
      RETURN
      ENDIF
      ILAST = ILAST + 1
      IDCYC = IDCYC + 1
      IF (IDCYC .GT. N30) IDCYC = 1
C === COMPUTE THE ERROR VECTOR
      CALL DIISER(FCR,FCI,FOR,FOI,FOCR,FOCI,DCR,DCI,DOR,DOI,
     +            DOCR,DOCI,SCRR1,SCRI1,SCRR2,SCRI2,SCRR3,SCRI3)
C === UPDATE THE DIIS MATRIX
      CALL DIISUP(DCR,DCI,DOR,DOI,DOCR,DOCI,SCRR1,SCRI1,SCRR2,SCRI2,
     +            SCRR3,SCRI3,DERR)
      IF ((ILAST - IFIRST) .GE. 1) THEN
C === SOLVE THE DIIS EQUATION
      CALL DIISIV(DERR)
C === COMPUTE THE NEW FOCK MATRIX
      CALL DIISUF(DCR,DCI,DOR,DOI,DOCR,DOCI,FCR,FCI,FOR,FOI,FOCR,FOCI)
      ENDIF
      CLOSE(DIISFIL,STATUS='KEEP')
      RETURN
      END
