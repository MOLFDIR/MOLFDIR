C
      SUBROUTINE RDIIS(ICYC,AR,AI,L,IRP,IOPT)
      IMPLICIT REAL*8 (A-H, O-Z)
C
C === THIS ROUTINE READS L ELEMENTS OF A FROM THE FILE DIISFIL
C === IDCYC, IRP AND IOPT ARE USED TO CALCULATE THE RECORD NUMBER
C === IOPT = 1 : CLOSED SHELL FOCK MATRIX
C === IOPT = 2 : OPEN SHELL FOCK MATRIX NR.1
C === IOPT = 3 : CLOSED SHELL ERROR VECTOR
C === IOPT = 4 : OPEN SHELL ERROR VECTOR NR.1
C === IOPT = 5 : B MATRIX
C === IOPT = 6 : INITIALIZATION, READ B MATRIX + ESSENTIAL COUNTERS
C === IOPT = 7 : OPEN SHELL FOCK MATRIX NR.2
C === IOPT = 8 : OPEN SHELL ERROR VECTOR NR.2
C
C === ICYC IS THE DIIS CYCLE NUMBER
C === AR AND AI FORM THE MATRIX TO BE READ FROM FILE
C === L IS THE LENGTH OF ONE ROW OF A IF IOPT = 1 OR 2
C ===   IS THE NUMBER OF ELEMENTS TO WRITTEN IF IOPT = 3 OR 4
C ===   IS IGNORED IF IOPT = 5 OR 6
C === IRP IS THE NUMBER OF THE CURRENT IRREP
C
C === THE STRUCTURE OF DIISFIL IS:
C ===   RECORD 1: DIMAT, IFIRST,ILAST,IDCYC,ICYCLS
C ===   N30 TIMES:
C ===     THE NEXT NDCREC RECORDS : THE CLOSED SHELL FOCK MATRIX
C ===     THE NEXT NDOREC RECORDS : THE OPEN SHELL FOCK MATRIX NR.1
C ===     THE NEXT NDTREC RECORDS : THE OPEN SHELL FOCK MATRIX NR.2
C ===     THE NEXT NDCREC RECORDS : THE CLOSED SHELL ERROR VECTOR
C ===     THE NEXT NDOREC RECORDS : THE OPEN SHELL ERROR VECTOR NR.1
C ===     THE NEXT NDTREC RECORDS : THE OPEN SHELL ERROR VECTOR NR.2
C
      REAL*8 AR(*), AI(*)
C
      INCLUDE 'paracom.inc'
      INCLUDE 'general.inc'
      INCLUDE 'diis.inc'
C
      IF (IOPT .EQ. 5) THEN
      READ(DIISFIL,REC=1) (AR(I),I=1,N31)
      ELSEIF (IOPT .EQ. 6) THEN
      READ(DIISFIL,REC=1) (AR(I),I=1,N31),
     +                     IFIRST,ILAST,IDCYC,ICYC
      ELSEIF (IOPT .EQ. 1) THEN
      IREC = (ICYC - 1) * (NDCREC+NDOREC+NDTREC)*2+INIRP(IRP) + 1
      READ(DIISFIL,REC=IREC)
     +    ((AR((J-1)*L+I),AI((J-1)*L+I),J=1,I),I=1,L)
      ELSEIF (IOPT .EQ. 2) THEN
      IREC = (ICYC - 1) * (NDCREC + NDOREC+NDTREC) * 2 +
     +       NDCREC + INIRP(16 + IRP) + 1
      READ(DIISFIL,REC=IREC)
     +    ((AR((J-1)*L+I),AI((J-1)*L+I),J=1,I),I=1,L)
      ELSEIF (IOPT .EQ. 3) THEN
      IREC = (2 * ICYC - 1) * (NDCREC+NDOREC+NDTREC)+INIRP(IRP) + 1
      READ(DIISFIL,REC=IREC) (AR(I),AI(I),I=1,L)
      ELSEIF (IOPT .EQ. 4) THEN
      IREC = (2 * ICYC - 1) * (NDCREC + NDOREC + NDTREC) +
     +       NDCREC + INIRP(16 + IRP) + 1
      READ(DIISFIL,REC=IREC) (AR(I),AI(I),I=1,L)
      ELSEIF (IOPT .EQ. 7) THEN
      IREC = (ICYC - 1) * (NDCREC + NDOREC+NDTREC) * 2 +
     +       NDCREC+NDOREC + INIRP(32 +IRP) + 1
      READ(DIISFIL,REC=IREC)
     +    ((AR((J-1)*L+I),AI((J-1)*L+I),J=1,I),I=1,L)
      ELSEIF (IOPT .EQ. 8) THEN
      IREC = (2 * ICYC - 1) * (NDCREC + NDOREC + NDTREC) +
     +       NDCREC+NDOREC + INIRP(32 + IRP) + 1
      READ(DIISFIL,REC=IREC) (AR(I),AI(I),I=1,L)
      ELSE
      CALL Q2JOB(6,' INVALID VALUE FOR IOPT IN RDIIS.')
      ENDIF
      RETURN
      END
