C
C     ===========================
      SUBROUTINE WRVEC (IRP,IRRP)
C     ===========================
      IMPLICIT REAL*8 (A-H, O-Z)
C
C     WRITE TRIAL VECTORS FOR DIRRCI TO FILE MRCTRIV
C     RECORD 1 : RECORD LENGTH,NUMBER OF VECTORS, ENERGIES
C     RECORD 2 : BIT REPRESENTATION DETERMINANTS
C     RECORD 3 : FIRST VECTOR
C     RECORD 4 : SECOND VECTOR, ETC.
C
      INCLUDE 'param.inc'
      INCLUDE 'common.inc'
      INCLUDE 'general.inc'
C
      CHARACTER*11 VECFIL
      DIMENSION DV (N2)
      NAMELIST/CIVEC/NVEC
C
      IF (NDET.EQ.0) RETURN
      DEGEN = 1.D-10
      NVEC = 5
      CALL LOCATE('CIVEC',5,ITEST)
      IF (ITEST.GT.0) READ (5,CIVEC)
      IF (NVEC.GT.NDET) NVEC = NDET
C
C     DETERMINE RECORD LENGTH
C
      LEN = MAX0(NDET,2)
      LENREC = 2 * LEN * 8
      VECFIL = 'MDTRIV_'//REPNA(IRRP)
      IF (VECFIL(8:8).EQ.'''') VECFIL(8:8) = '1'
      IF (VECFIL(8:8).EQ.'"') VECFIL(8:8) = '2'
      OPEN (10,FILE=VECFIL,ACCESS='DIRECT',RECL=LENREC)
      WRITE (10,REC=2) NDET,(IDET(I),I=1,NDET)
      IVEC = 0
      EPREV = D(1,IRP) - DEGEN - 1.D0
      DO 10 I = 1, NDET
C     IF (D(I,IRP)-EPREV.GT.DEGEN) THEN
         IVEC = IVEC + 1
         DV (IVEC) = D(I,IRP) + ECORE
         WRITE (10,REC=IVEC+2) (HR(J,I),HI(J,I),J=1,NDET)
         EPREV = D(I,IRP)
C     ENDIF
      IF (IVEC.EQ.NVEC) GOTO 100
   10 CONTINUE
  100 WRITE (10,REC=1) LENREC,NVEC,(DV(IVEC),IVEC=1,NVEC)
      CLOSE (10)
      WRITE (6,1000) REPNA(IRRP),NVEC,VECFIL,(DV(IVEC),IVEC=1,NVEC)
      RETURN
 1000 FORMAT (/' REPRESENTATION ',A4,'; ',I2,
     &' VECTORS WRITTEN TO FILE ',A12//' EIGENVALUES ',(T20,F20.12))
      END
